;   Copyright (c) MURANGWA Pacifique. and affiliates.
;   This source code is licensed under the Apache License 2.0 found in the
;   LICENSE file in the root directory of this source tree.


;   Kin Programming Language's Grammar in BNF (Backus-Naur Form)


    <program> ::= {<statement> ";"}*

    <statement> ::= <declaration> | <assignment> | <expression> | <conditional> | <loop> | <function> | <file_operation> | <pointers> | <system_command> | <type_casting> | <unary_operation> | <input_statement> | <output_statement>

    <declaration> ::= "reka" <variable> "=" <value> | <pointer-decralation>
    
    <value> ::= <factor>

    <assignment> ::= <variable> "=" <expression> | <function_call> | <input_statement>

    <expression> ::= <term> {<binary_operator> <expression>}

    <term> ::= <factor> {<binary_operator> <factor>}

    <factor> ::= <number> | <variable> | <string> | "(" <expression> ")" | <function_call>

    <conditional> ::= "niba" "(" <condition> ")" "{" <block> "}" [{"nanone_niba" "(" <condition> ")" "{" <block> "}"}*] ["niba_byanze" "{" <block> "}"]

    <condition> ::= <logical_condition> | nibyo | sibyo

    <logical_condition> ::= <expression> <logical_operator> <expression> | "(" <logical_condition> ")"

    <logical_operator> ::= "&&" | "||" | "==" | "!=" | "<" | "<=" | ">" | ">=" | "!"

    <loop> ::= "niba" "(" <loop-conditions> ")" "subiramo" "{" <statement>* "}"

    <loop-conditions> ::= <condition>

    <function> ::= "porogaramu_ntoya" <function_name> "(" <parameter_list> ")" <function-block>

    <parameter_list> ::= <parameter> {"," <parameter>}

    <parameter> ::= <variable> | <value>

    <function-block> :: = "{" <block> ["tanga" <expression>]  "}"

    <file_operation> ::= <file_read> | <file_write> | <file_append>

    <file_read> ::= "soma_inyandiko" "(" <string> ")"

    <file_write> ::= "andika_inyandiko" "(" <string> "," <variable> ")"

    <file_append> ::= "vugurura_inyandiko" "(" <string> "," <string> ")"

    <pointers> ::= <pointer-decralation> | <accessing-pointer>

    <pointer-decralation> ::= "reka" "*" <variable> "=" "&" <variable> ";"

    <accessing-pointer> ::= "*" <variable>

    <system_command> ::= "sisitemu" "(" <string> ")"

    <type_casting> ::= <casting_function> "(" <value> ")"

    <casting_function> ::= "umubare" | "umubare_wibice" | "ijambo"

    <unary_operation> ::= <unary_operator> <value>

    <unary_operator> ::= "++" | "--" | "!"

    <input_statement> ::= "injiza_amakuru" "(" <string> ")"
    
    <output_statement> ::= "tangaza_amakuru" "(" <string> ")"

    <ubwoko> ::= "ubwoko" "(" <variable> ")"

    <binary_operator> ::= "+" | "-" | "*" | "/" | "%" |  "^" | "&&" | "||" | ">" | ">=" | "<" | "<=" | "==" | "!="

    <variable> ::= <identifier>

    <function_call> ::= <function_name> "(" <argument_list> ")"

    <function_name> ::= <identifier>

    <argument_list> ::= <expression> {"," <expression>}

    <block> ::= {<statement> ";"}*

    <number> ::= <integer> | <float>

    <integer> ::= <digit>+

    <float> ::= <digit>+ "." <digit>+

    <digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

    <string> ::= <string_element> | <string> "+" <string_element>

    <string_element> ::= "\"" <characters> "\""

    <list> ::= "[" {<expression> ","}* <expression> "]"

    <structure> ::= "{" {<field> ","}* <field> "}"

    <field> ::= <identifier> ":" <expression>

    <identifier> ::= <letter> {<letter> | <digit> | "_"}

    <letter> ::= "a" | "b" | ... | "z" | "A" | "B" | ... | "Z"

    <characters> ::= <character> {<character>}

    <character> ::= <letter> | <digit> | <symbol>

    <symbol> ::= "~" | "`" | "!" | "@" | "#" | ... | "/"
